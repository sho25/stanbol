/*
* Licensed to the Apache Software Foundation (ASF) under one or more
* contributor license agreements.  See the NOTICE file distributed with
* this work for additional information regarding copyright ownership.
* The ASF licenses this file to You under the Apache License, Version 2.0
* (the "License"); you may not use this file except in compliance with
* the License.  You may obtain a copy of the License at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
/**
 * JavaCC template file created by SF JavaCC plugin 1.5.17+ wizard for JavaCC 1.5.0+
 */
options
{
  JDK_VERSION = "1.6";
  static = false;
}

PARSER_BEGIN(RuleParser)
package org.apache.stanbol.rules.manager.parse;

import java.io.Reader;
import java.io.StringReader;
import java.net.URI;
import java.net.URISyntaxException;
import java.util.Iterator;

import com.hp.hpl.jena.rdf.model.ModelFactory;
import com.hp.hpl.jena.rdf.model.Resource;


import org.apache.stanbol.rules.base.api.Rule;
import org.apache.stanbol.rules.base.api.RuleAtom;
import org.apache.stanbol.rules.base.api.URIResource;
import org.apache.stanbol.rules.base.api.util.AtomList;
import org.apache.stanbol.rules.manager.KB;
import org.apache.stanbol.rules.manager.RuleImpl;
import org.apache.stanbol.rules.manager.atoms.ClassAtom;
import org.apache.stanbol.rules.manager.atoms.ComparisonAtom;
import org.apache.stanbol.rules.manager.atoms.DatavaluedPropertyAtom;
import org.apache.stanbol.rules.manager.atoms.EndsWithAtom;
import org.apache.stanbol.rules.manager.atoms.IndividualPropertyAtom;
import org.apache.stanbol.rules.manager.atoms.DifferentAtom;
import org.apache.stanbol.rules.manager.atoms.TypedLiteralAtom;
import org.apache.stanbol.rules.manager.atoms.LengthAtom;
import org.apache.stanbol.rules.manager.atoms.NotAtom;
import org.apache.stanbol.rules.manager.atoms.NumberAtom;
import org.apache.stanbol.rules.manager.atoms.NumericFunctionAtom;
import org.apache.stanbol.rules.manager.atoms.SPARQLddAtom;
import org.apache.stanbol.rules.manager.atoms.SameAtom;
import org.apache.stanbol.rules.manager.atoms.LessThanAtom;
import org.apache.stanbol.rules.manager.atoms.GreaterThanAtom;
import org.apache.stanbol.rules.manager.atoms.ResourceAtom;
import org.apache.stanbol.rules.manager.atoms.VariableAtom;
import org.apache.stanbol.rules.manager.atoms.LetAtom;
import org.apache.stanbol.rules.manager.atoms.ConcatAtom;
import org.apache.stanbol.rules.manager.atoms.StringAtom;
import org.apache.stanbol.rules.manager.atoms.StringFunctionAtom;
import org.apache.stanbol.rules.manager.atoms.SubstringAtom;
import org.apache.stanbol.rules.manager.atoms.SubtractionAtom;
import org.apache.stanbol.rules.manager.atoms.SumAtom;
import org.apache.stanbol.rules.manager.atoms.NewNodeAtom;
import org.apache.stanbol.rules.manager.atoms.BlankNodeAtom;
import org.apache.stanbol.rules.manager.atoms.UpperCaseAtom;
import org.apache.stanbol.rules.manager.atoms.LowerCaseAtom;
import org.apache.stanbol.rules.manager.atoms.NamespaceAtom;
import org.apache.stanbol.rules.manager.atoms.LocalNameAtom;
import org.apache.stanbol.rules.manager.atoms.StrAtom;
import org.apache.stanbol.rules.manager.atoms.UnionAtom;
import org.apache.stanbol.rules.manager.atoms.CreateLabelAtom;
import org.apache.stanbol.rules.manager.atoms.PropStringAtom;
import org.apache.stanbol.rules.manager.atoms.IsBlankAtom;
import org.apache.stanbol.rules.manager.atoms.SPARQLcAtom;
import org.apache.stanbol.rules.manager.atoms.SPARQLdAtom;
import org.apache.stanbol.rules.manager.atoms.StartsWithAtom;
import org.apache.stanbol.rules.base.api.RuleExpressiveness;

public class RuleParser
{

  static KB kb;
  
  public static KB parse( String inString ) {
  {
  	kb = new KB();
  	Reader reader = new StringReader( inString ) ;
    RuleParser parser = new RuleParser(reader);
    StringBuffer buffer = new StringBuffer() ;
	try {
		parser.start( ) ; 
	} catch( TokenMgrError e ) { 
		throw new IllegalStateException(e) ; 
	} catch( ParseException e ) { 
		throw new IllegalStateException(e) ; 
	} 
	return kb ; }
  }


	private static URI getSWRLArgument(String argument){
                Resource rdfNode = null;
                String[] argumentComposition = argument.split(":");
                if(argumentComposition.length == 2){
                        String prefix = argumentComposition[0];
                        String resourceName = argumentComposition[1];

                        String namespaceURI = kb.getPrefixURI(prefix);
                        rdfNode = ModelFactory.createDefaultModel().createResource(namespaceURI+resourceName);
                        try {
							return new URI(rdfNode.getURI());
						} catch (URISyntaxException e) {
							// TODO Auto-generated catch block
							e.printStackTrace();
						}

                }
                
                return null;
        }

        private static URI getSWRLVariable(String argument){
                Resource variableResource = null;
                String variableString = argument.substring(1);


                variableResource = ModelFactory.createDefaultModel().createResource(kb.getPrefixURI("var")+variableString);



                try {
					return new URI(variableResource.getURI());
				} catch (URISyntaxException e) {
					// TODO Auto-generated catch block
					e.printStackTrace();
					return null;
				}
        }
}

PARSER_END(RuleParser)

SKIP : {" "}
SKIP : {"\r" | "\t" | "\n"}

TOKEN : /* OPERATORS */
{
  < LARROW : "->" >
| < COLON : ":" >
| < EQUAL : "=" >
| < AND : "." >
| < COMMA : "," >
| < REFLEXIVE : "+" >
| < SAME : "same" >
| < DIFFERENT : "different" >
| < LESSTHAN : "lt" >
| < GREATERTHAN : "gt" >
| < IS : "is" >
| < NEW_NODE : "newNode" >
| < LENGTH : "length" >
| < SUBSTRING : "substring" >
| < UPPERCASE : "upperCase" >
| < LOWERCASE : "lowerCase" >
| < STARTS_WITH : "startsWith" >
| < ENDS_WITH : "endsWith" >
| < LET : "let" >
| < CONCAT : "concat" >
| < HAS : "has" >
| < VALUES : "values" >
| < NOTEX : "notex" >
| < PLUS : "sum" >
| < MINUS : "sub" >
| < NOT : "not" >
| < NAMESPACE : "namespace" >
| < LOCALNAME : "localname" >
| < STR : "str" >
| < APOX : "^" >
| < UNION : "union" >
| < CREATE_LABEL : "createLabel" >
| < SPARQL_C : "sparql-c" >
| < SPARQL_D : "sparql-d" >
| < SPARQL_DD : "sparql-dd" >
| < PROP : "prop" >
| < IS_BLANK : "isBlank" >
| < FORWARD_CHAIN : "!" >
}

TOKEN : /* BLOCKS */
{
  < LPAR : "(" >
| < RPAR : ")" >
| < DQUOT : "\"" >
| < LQUAD : "[" >
| < RQUAD : "]" >
}

TOKEN :
{
  < NUM : ([ "0"-"9"])+ > 
| < VAR : ([ "0"-"9","a"-"z","A"-"Z","-", "_", "."])+ >
| < VARIABLE : "?" ([ "0"-"9","a"-"z","A"-"Z","-", "_"])+ >
| < URI : "<" ([ "0"-"9","a"-"z","A"-"Z","-", "_", ".", "#", ":", "/", "(", ")" ])+ ">" >
| < STRING : "\"" ([ "0"-"9","a"-"z","A"-"Z","-", "_", ".", ":", "/", "#", "\\", "?", " ", "!", "$", "%" ])+ "\"" >
| < SPARQL_STRING : "%" ([ "0"-"9","a"-"z","A"-"Z","-", "_", ".", ":", "/", "#", "\\", "?", " ", "!", "$", "%", "{", "}", "(", ")", "\"", "<", ">", "=", "+", "\n", "\t", "&", "|", "," ])+ "%" >
| < BNODE : "_:" ([ "0"-"9","a"-"z","A"-"Z","-", "_", "."])+ >
}


void start () :
{}
{
	expression() expressionCont()
}

void expressionCont() :
{}
{
	( < AND > expression() ) | {}
}

void expression() :
{Rule rule;}
{
	prefix() expressionCont()  
}

void prefix() :
{String nsPrefix; Object obj; Rule rule;}
{
  nsPrefix=getVariable() (obj=equality() { String prefixURI = (String)obj;
  										   prefixURI = prefixURI.substring(1, prefixURI.length()-1);
  										   kb.addPrefix(nsPrefix, prefixURI);  										    
  										 } 
  							|   obj=rule() { AtomList[] atoms = (AtomList[]) obj;
  											 String varPrefix = kb.getPrefixURI("var");
  											 varPrefix = varPrefix.substring(0, varPrefix.length());
  											 
  											 if(atoms.length == 1){
                                                            AtomList body = atoms[0];
                                                            if(body.size() == 1){
                                                                    Iterator<RuleAtom> it = body.iterator();
                                                                    RuleAtom atom = it.next();
                                                                    if(atom.isSPARQLConstruct()){
                                                                            rule = new RuleImpl(varPrefix+nsPrefix, atoms[0], null, RuleExpressiveness.SPARQLConstruct);
                                                                            kb.addRule(rule);
                                                                    }
                                                                    else if(atom.isSPARQLDelete()){
                                                                            rule = new RuleImpl(varPrefix+nsPrefix, atoms[0], null, RuleExpressiveness.SPARQLDelete);
                                                                            kb.addRule(rule);
                                                                    }
                                                                    else if(atom.isSPARQLDeleteData()){
                                                                            rule = new RuleImpl(varPrefix+nsPrefix, atoms[0], null, RuleExpressiveness.SPARQLDeleteData);
                                                                            kb.addRule(rule);
                                                                    }
                                                            }

                                                     }
                                                 else{
                                                         rule = new RuleImpl(varPrefix+nsPrefix, atoms[0], atoms[1], RuleExpressiveness.KReSCore);
                                                         kb.addRule(rule);
                                                }
	  									}
  						)
  						
|

	< FORWARD_CHAIN > nsPrefix=getVariable() obj=rule() { AtomList[] atoms = (AtomList[]) obj;
  											 String varPrefix = kb.getPrefixURI("var");
  											 varPrefix = varPrefix.substring(0, varPrefix.length());
  											 rule = new RuleImpl(varPrefix+nsPrefix, atoms[0], atoms[1], RuleExpressiveness.ForwardChaining);
  											 kb.addRule(rule); }
  											 
|	< REFLEXIVE > nsPrefix=getVariable() obj=rule() {  	AtomList[] kReSAtoms = (AtomList[]) obj;
														 String pref = kb.getPrefixURI("var");
                                                         pref = pref.substring(0, pref.length());
                                                         rule = new RuleImpl(pref+nsPrefix, kReSAtoms[0], kReSAtoms[1], RuleExpressiveness.Reflexive);
                                                         kb.addRule(rule); }
}


String equality() :
{String nsURI;}
{
	< EQUAL > ( nsURI=getURI() )
	{ return nsURI;}
}



AtomList[] rule() :
{AtomList[] ruleAtoms;}
{
   < LQUAD >  ruleAtoms=ruleDefinition()  < RQUAD >
   { return ruleAtoms; }
  
}

AtomList[] ruleDefinition():
{AtomList body; AtomList head; Token t;}
{
	body=atomList() <LARROW> head=atomList()
	{ return new AtomList[]{body, head};}
	
|	
	< SPARQL_C > < LPAR > t = < SPARQL_STRING > < RPAR >
	{ 
		RuleAtom sparqlAtom = new SPARQLcAtom(t.image);
		AtomList atomList = new AtomList();
		atomList.addToHead(sparqlAtom);
		return new AtomList[]{atomList};
	}
	
|	
	< SPARQL_D > < LPAR > t = < SPARQL_STRING > < RPAR >
	{ 
		RuleAtom sparqlDAtom = new SPARQLdAtom(t.image);
        AtomList atomDList = new AtomList();
        atomDList.addToHead(sparqlDAtom);
        return new AtomList[]{atomDList};
	}

|	
	< SPARQL_DD > < LPAR > t = < SPARQL_STRING > < RPAR >
	{ 
		RuleAtom sparqlDDAtom = new SPARQLddAtom(t.image);
        AtomList atomDDList = new AtomList();
        atomDDList.addToHead(sparqlDDAtom);
        return new AtomList[]{atomDDList};
	} 
}

AtomList atomList() :
{AtomList atomList = new AtomList(); RuleAtom kReSAtom;}
{
	kReSAtom=atom() atomList=atomListRest() 
	{ atomList.addToHead(kReSAtom); return atomList;}
	
	| 
	{}
	{return atomList;} 
}


AtomList atomListRest() :
{AtomList atomList = new AtomList(); RuleAtom kReSAtom;}
{
	< AND > atomList=atomList() 
	{ return atomList;}
	| 
	{}
	{return atomList;} 
}


RuleAtom atom() :
{RuleAtom ruleAtom;}
{
   ruleAtom=classAtom() {return ruleAtom;}
|  ruleAtom=individualPropertyAtom() {return ruleAtom;}
|  ruleAtom=datavaluedPropertyAtom() {return ruleAtom;}
|  ruleAtom=letAtom() {return ruleAtom;}
|  ruleAtom=newNodeAtom() {return ruleAtom;}
|  ruleAtom=comparisonAtom() {return ruleAtom;}
|  ruleAtom=unionAtom() {return ruleAtom;}

}

RuleAtom unionAtom() : 
{AtomList atomList1; AtomList atomList2;}
{
	< UNION > < LPAR > atomList1 = atomList() < COMMA > atomList2 = atomList() < RPAR >
	{ return new UnionAtom(atomList1, atomList2); }
}

StringFunctionAtom createLabelAtom() : 
{StringFunctionAtom stringFunctionAtom;}
{
	< CREATE_LABEL > < LPAR > stringFunctionAtom = stringFunctionAtom() < RPAR >
	{ return new CreateLabelAtom(stringFunctionAtom); }
}


StringFunctionAtom propStringAtom() : 
{StringFunctionAtom stringFunctionAtom1; StringFunctionAtom stringFunctionAtom2;}
{
	< PROP > < LPAR > stringFunctionAtom1 = stringFunctionAtom() < COMMA > stringFunctionAtom2 = stringFunctionAtom() < RPAR >
	{ return new PropStringAtom(stringFunctionAtom1, stringFunctionAtom2); }
}




ComparisonAtom endsWithAtom() : 
{RuleAtom ruleAtom; StringFunctionAtom arg; StringFunctionAtom stringFunctionAtom;}
{
	< ENDS_WITH > < LPAR > arg=stringFunctionAtom() < COMMA > stringFunctionAtom = stringFunctionAtom()< RPAR >

	{return new EndsWithAtom(arg, stringFunctionAtom);}
}


ComparisonAtom startsWithAtom() : 
{RuleAtom ruleAtom; StringFunctionAtom arg; StringFunctionAtom stringFunctionAtom;}
{
	< STARTS_WITH > < LPAR > arg=stringFunctionAtom() < COMMA > stringFunctionAtom = stringFunctionAtom()< RPAR >

	{return new StartsWithAtom(arg, stringFunctionAtom);}
}

StringFunctionAtom stringFunctionAtom() : 
{Object obj; StringFunctionAtom stringFunctionAtom;}
{
	(
		stringFunctionAtom=concatAtom()
	|   stringFunctionAtom=upperCaseAtom()
	|   stringFunctionAtom=lowerCaseAtom()
	|   stringFunctionAtom=substringAtom()
	|   stringFunctionAtom=namespaceAtom()
	|   stringFunctionAtom=localnameAtom()
	|   stringFunctionAtom=strAtom()
	|   stringFunctionAtom=stringAtom()
	| 	stringFunctionAtom=propStringAtom()
	|	stringFunctionAtom=createLabelAtom()
	)

	{return stringFunctionAtom;}
}


StrAtom strAtom() : 
{URIResource uri;}
{
	< STR > < LPAR > uri = iObject() < RPAR >
	{return new StrAtom(uri);}
}

NamespaceAtom namespaceAtom() : 
{URIResource uri;}
{
	< NAMESPACE > < LPAR > uri = iObject() < RPAR >
	{return new NamespaceAtom(uri);}
}

LocalNameAtom localnameAtom() : 
{URIResource uri;}
{
	< LOCALNAME > < LPAR > uri = iObject() < RPAR >
	{return new LocalNameAtom(uri);}
}

StringAtom stringAtom() :
{Object obj; StringFunctionAtom stringFunctionAtom;}
{
	obj = uObject() { return new StringAtom(obj.toString()); }
}

ConcatAtom concatAtom() : 
{StringFunctionAtom arg1; StringFunctionAtom arg2;}
{
	< CONCAT > < LPAR > arg1=stringFunctionAtom()  < COMMA > arg2=stringFunctionAtom() < RPAR >
	{ return new ConcatAtom(arg1, arg2);}
}


UpperCaseAtom upperCaseAtom() : 
{StringFunctionAtom arg;}
{
	< UPPERCASE > < LPAR > arg=stringFunctionAtom() < RPAR >
	{ return new UpperCaseAtom(arg);}
}

LowerCaseAtom lowerCaseAtom() : 
{StringFunctionAtom arg;}
{
	< LOWERCASE > < LPAR > arg=stringFunctionAtom() < RPAR >
	{ return new LowerCaseAtom(arg);}
}

SubstringAtom substringAtom() : 
{StringFunctionAtom arg; NumericFunctionAtom start; NumericFunctionAtom length;}
{
	< SUBSTRING > < LPAR > arg=stringFunctionAtom() < COMMA > start=numericFunctionAtom() < COMMA > length=numericFunctionAtom() < RPAR >
	{ return new SubstringAtom(arg, start, length);}
}


NumericFunctionAtom numericFunctionAtom() : 
{NumericFunctionAtom numericFunctionAtom;}
{
	(
		numericFunctionAtom = sumAtom()
	|   numericFunctionAtom = subtractionAtom()
	|   numericFunctionAtom = lengthAtom()
	|   numericFunctionAtom = numberAtom()
	)

	{return numericFunctionAtom;}
}


LengthAtom lengthAtom() : 
{StringFunctionAtom stringFunctionAtom;}
{
	< LENGTH > < LPAR > stringFunctionAtom = stringFunctionAtom() < RPAR >
	{return new LengthAtom(stringFunctionAtom);}
}

SumAtom sumAtom() : 
{NumericFunctionAtom numericFunctionAtom1; NumericFunctionAtom numericFunctionAtom2;}
{
	
	< PLUS > < LPAR > numericFunctionAtom1 = numericFunctionAtom() < COMMA > numericFunctionAtom2 = numericFunctionAtom() < RPAR >

	{return new SumAtom(numericFunctionAtom1, numericFunctionAtom2);}
}

SubtractionAtom subtractionAtom() : 
{NumericFunctionAtom numericFunctionAtom1; NumericFunctionAtom numericFunctionAtom2;}
{
	
	< MINUS > < LPAR > numericFunctionAtom1 = numericFunctionAtom() < COMMA > numericFunctionAtom2 = numericFunctionAtom() < RPAR >

	{return new SubtractionAtom(numericFunctionAtom1, numericFunctionAtom2);}
}

NumericFunctionAtom numberAtom() :
{Token t;}
{
	(
		t = < NUM >	
	|	t = < VARIABLE >
	)
	{ return new NumberAtom(t.image); }

}


ClassAtom classAtom() :
{URIResource uri1; URIResource uri2;}
{
	< IS > < LPAR > uri1=iObject()  < COMMA > uri2=iObject() < RPAR >
	{ return new ClassAtom(uri1, uri2);}
}


NewNodeAtom newNodeAtom() : 
{URIResource arg1; Object arg2;}
{
	< NEW_NODE > < LPAR > arg1=iObject()  < COMMA > arg2=dObject() < RPAR >
	{ return new NewNodeAtom(arg1, arg2);}
}

LetAtom letAtom() : 
{URIResource uri1; StringFunctionAtom fun;}
{
	< LET > < LPAR > uri1=iObject()  < COMMA > fun=stringFunctionAtom() < RPAR >
	{ return new LetAtom(uri1, fun);}
}


IndividualPropertyAtom individualPropertyAtom() :
{URIResource uri1; URIResource uri2; URIResource uri3;}
{
	 < HAS > < LPAR > uri1=iObject() < COMMA > uri2=iObject() <COMMA> uri3=iObject() < RPAR >
	 {return new IndividualPropertyAtom(uri1, uri2, uri3);}
}


DatavaluedPropertyAtom datavaluedPropertyAtom() :
{URIResource uri1; URIResource uri2; Object obj;}
{
	 < VALUES > < LPAR > uri1=iObject() < COMMA > uri2=iObject() <COMMA> obj=dObject() < RPAR >
	 { return new DatavaluedPropertyAtom(uri1, uri2, obj); }
}


SameAtom sameAsAtom() :
{StringFunctionAtom stringFunctionAtom1; StringFunctionAtom stringFunctionAtom2;}
{
	< SAME > < LPAR > stringFunctionAtom1=stringFunctionAtom() < COMMA > stringFunctionAtom2=stringFunctionAtom() < RPAR >
	{ return new SameAtom(stringFunctionAtom1, stringFunctionAtom2); } 
}

LessThanAtom lessThanAtom() :
{Object obj1; Object obj2;}
{
	< LESSTHAN > < LPAR > obj1=dObject() < COMMA > obj2=dObject() < RPAR >
	{ return new LessThanAtom(obj1, obj2); } 
}

GreaterThanAtom greaterThanAtom() :
{Object obj1; Object obj2;}
{
	< GREATERTHAN > < LPAR > obj1=dObject() < COMMA > obj2=dObject() < RPAR >
	{ return new GreaterThanAtom(obj1, obj2); } 
}

DifferentAtom differentFromAtom() :
{StringFunctionAtom stringFunctionAtom1; StringFunctionAtom stringFunctionAtom2;}
{
	< DIFFERENT > < LPAR > stringFunctionAtom1=stringFunctionAtom() < COMMA > stringFunctionAtom2=stringFunctionAtom() < RPAR >
	{ return new DifferentAtom(stringFunctionAtom1, stringFunctionAtom2); } 
}

URIResource reference() :
{ String uri1;
  Token colon;
  String uri3; }
{
	 uri1=getURI() { uri1 = uri1.substring(1, uri1.length()-1);
	 					try {
						  return new ResourceAtom(new URI(uri1));
							} catch (URISyntaxException e) {
								e.printStackTrace();
							} } | 
	 uri1= getVariable() colon=< COLON > uri3=getVariable() { return new ResourceAtom(getSWRLArgument(uri1+colon.image+uri3)); }
}



URIResource varReference() :
{ String uri1;
  Token colon;
  String uri3; }
{
	 uri1=getURI() { try {
															return new ResourceAtom(new URI(uri1));
														} catch (URISyntaxException e) {
															e.printStackTrace();
														} } | 
	 uri1= getVariable() colon=< COLON > uri3=getVariable() { return new ResourceAtom(getSWRLArgument(uri1+colon.image+uri3)); }
}


String getURI() :
{
	Token t;
}
{
	t = < URI > { return t.image; }
}

String getVariable() :
{
	Token t;
}
{
	t = < VAR > { return t.image; }
}


String getString() :
{
	Token t;
}
{
	t = < STRING > { return t.image; }
}

Integer getInt() :
{
	Token t;
}
{
	t=< NUM > { return Integer.valueOf(t.image); }
}


Object uObject() :
{ Object obj; }
{
	(
		obj = variable() 
		| obj = reference()
		| obj = getString() 
		| obj = getInt()
	)
	{return obj;}
	
}


URIResource iObject() :
{ URIResource uri; }
{
	uri = variable() {return uri;} | uri = reference() {return uri;}
}

Object dObject() :
{ Object variable; }
{
	
	(variable=literal() | variable=variable()) {return variable;}	
}



Object literal() : 
{ Object literal; URIResource typedLiteral;}
{
	(
	
			literal=getString() typedLiteral=typedLiteral() 
		|   literal=getInt() typedLiteral=typedLiteral()
		
	)
	
	{
		if(typedLiteral != null){
			return new TypedLiteralAtom(literal, typedLiteral);
		}
		else{
			return literal;
		}
	}
	
}

URIResource typedLiteral() :
{ URIResource type = null; }
{
	(
			< APOX > < APOX > type = reference()
		| {}
	)
	{return type;}
} 

URIResource variable() :
{ Token t; String var;}
{
	< NOTEX > < LPAR > t = < VARIABLE > < RPAR > {var=t.image; var=kb.getPrefixURI("var") + var.substring(1);
														try{ 
															return new VariableAtom(new URI(var), true);
														} catch (URISyntaxException e) {
															e.printStackTrace();
															return null;
														}}
|	t = < VARIABLE >
	{ var=t.image; var=kb.getPrefixURI("var") + var.substring(1);
														try{ 
															return new VariableAtom(new URI(var), false);
														} catch (URISyntaxException e) {
															e.printStackTrace();
															return null;
														} }
|	t = < BNODE >
	{ var=t.image; 	return new RuleBlankNode(var); }
}

ComparisonAtom notAtom() :
{ ComparisonAtom comparisonAtom; }
{
	< NOT > < LPAR > comparisonAtom = comparisonAtom() < RPAR >
	{return new NotAtom(comparisonAtom);}	
}

ComparisonAtom isBlankAtom() :
{ URIResource uriRes; }
{
	< IS_BLANK > < LPAR > uriRes = iObject()  < RPAR >
	{return new IsBlankAtom(uriRes);}	
}

ComparisonAtom comparisonAtom() :
{ ComparisonAtom comparisonAtom; }
{
	(
		comparisonAtom = sameAsAtom()
	|	comparisonAtom = lessThanAtom()
	|	comparisonAtom = greaterThanAtom()
	|	comparisonAtom = differentFromAtom()
	|   comparisonAtom = notAtom()
	|   comparisonAtom = startsWithAtom()
	|   comparisonAtom = endsWithAtom()
	|   comparisonAtom = isBlankAtom()
	) 
	{return comparisonAtom;}
}


